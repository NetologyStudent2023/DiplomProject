


#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции.

// Получает на вход текстовое сообщение и отпарвляет его в телеграм-бот
// 
// Параметры:
// ТекстСообщения - строка - текст сообщения
// 
// Возвращаемое значение:
// Булево - подтверждение отправки уведомления
//
Функция ОтправитьСообщениеВТелеграмБот(ТекстСообщения) Экспорт
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	
	ЗапросHTTP = Новый HTTPЗапрос();
	ЗапросHTTP.АдресРесурса = "bot" + Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить() + "/" + "sendMessage";
	ЗапросHTTP.Заголовки.Вставить("Content-Type","application/json"); 
	
	СтруктураСообщения = Новый Структура();
	СтруктураСообщения.Вставить("chat_id", Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить()); 
	СтруктураСообщения.Вставить("text", ТекстСообщения);
	
	ЗапросHTTP.УстановитьТелоИзСтроки(СтрокаJSON(СтруктураСообщения));
	
	ОтветHTTP = Соединение.ОтправитьДляОбработки(ЗапросHTTP);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		Возврат Истина;
	Иначе
		ТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
		ЗаписьЖурналаРегистрации("ОбменСТелеграмБотом", УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()+ТелоОтвета));
		Возврат Ложь;
	КонецЕсли;

КонецФункции

// Преобразовывает подготовленный объект в строку JSON
// 
// Параметры:
// ОбъектJSON - структура - подготовленный объект
// 
// Возвращаемое значение:
// ЗаписьJSON - преобразованная строка
//
Функция СтрокаJSON(ОбъектJSON) Экспорт
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, ОбъектJSON);
	
	Возврат Запись.Закрыть();
	
КонецФункции

// Преобразовывает строку JSON в объект
// 
// Параметры:
// СтрокаJSON - строка - строка для преобразования
// 
// Возвращаемое значение:
// ЧтениеJSON - преобразованный объект
//
Функция ОбъектJSON(СтрокаJSON) Экспорт
	
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(СтрокаJSON);
	ОбъектJSON = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	
	Возврат ОбъектJSON;
	
КонецФункции

// Метод регламентного задания,
// Направляет уведомления о новых и измененных заказах в телеграм-бот
// 
Процедура НаправитьУведомление() Экспорт
	
	Выборка = Справочники.ВКМ_УведомленияТелеграмБоту.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Отправлено = ОтправитьСообщениеВТелеграмБот(Выборка.Текст);
		
		Если Отправлено Тогда
			ВыборкаОбъект = Выборка.ПолучитьОбъект();
			ВыборкаОбъект.Удалить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти
